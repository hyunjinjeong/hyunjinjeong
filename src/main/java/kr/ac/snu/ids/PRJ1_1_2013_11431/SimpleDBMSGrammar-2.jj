options
{
  IGNORE_CASE = true;
}

PARSER_BEGIN(SimpleDBMSParser)
package kr.ac.snu.ids.PRJ1_1_2013_11431;

import java.io.UnsupportedEncodingException;
import java.io.File;

import com.sleepycat.je.Database;
import com.sleepycat.je.DatabaseException;
import com.sleepycat.je.DatabaseConfig;
import com.sleepycat.je.DatabaseEntry;
import com.sleepycat.je.Cursor;
import com.sleepycat.je.LockMode;
import com.sleepycat.je.OperationStatus;
import com.sleepycat.je.Environment;
import com.sleepycat.je.EnvironmentConfig;

import kr.ac.snu.ids.PRJ1_1_2013_11431.Message;

public class SimpleDBMSParser
{
  // Constants to print messages.
  public static final int PRINT_SYNTAX_ERROR = 0;
  public static final int PRINT_CREATE_TABLE = 1;
  public static final int PRINT_DROP_TABLE = 2;
  public static final int PRINT_DESC = 3;
  public static final int PRINT_SELECT = 4;
  public static final int PRINT_INSERT = 5;
  public static final int PRINT_DELETE = 6;
  public static final int PRINT_SHOW_TABLES = 7;
    
  public static void main(String args[]) throws ParseException
  {
    // Environment & Database define
    Environment myDbEnvironment = null;
    Database myDatabase = null;

    /* Open DB */
    // Open Database Environment or if not, create one.
    EnvironmentConfig envConfig = new EnvironmentConfig();
    envConfig.setAllowCreate(true);
    myDbEnvironment = new Environment(new File("db/"), envConfig);

    // Open Database or if not, create one.
    DatabaseConfig dbConfig = new DatabaseConfig();
    dbConfig.setAllowCreate(true);
    dbConfig.setSortedDuplicates(true);
    myDatabase = myDbEnvironment.openDatabase(null, "sampleDatabase", dbConfig);

    Cursor cursor = null;
    DatabaseEntry key;
    DatabaseEntry value;

    try {
      cursor = myDatabase.openCursor(null, null);
      key = new DatabaseEntry("key".getBytes("UTF-8"));
      value = new DatabaseEntry("value".getBytes("UTF-8"));
    }
    catch (DatabaseException de) {
    }
    catch (UnsupportedEncodingException e) {
      e.printStackTrace();
    }
    finally {
      cursor.close();
    }

    // Find keys & values.
    DatabaseEntry foundKey;
    DatabaseEntry foundValue;
    try {
      cursor = myDatabase.openCursor(null, null);
      foundKey = new DatabaseEntry("key".getBytes("UTF-8"));
      foundValue = new DatabaseEntry("value".getBytes("UTF-8"));
      cursor.getFirst(foundKey, foundValue, LockMode.DEFAULT);
      do {
        String keyString = new String(foundKey.getData(), "UTF-8");
        String valueString = new String(foundValue.getData(), "UTF-8");
//        System.out.println(keyString);
      } while (cursor.getNext(foundKey, foundValue, LockMode.DEFAULT) == OperationStatus.SUCCESS);
    }
    catch (DatabaseException de) {
    }
    catch (UnsupportedEncodingException e) {
      e.printStackTrace();
    }
    finally {
      cursor.close();
    }

    // Close DB
    if (myDatabase != null) myDatabase.close();
    if (myDbEnvironment != null) myDbEnvironment.close();
    
    // Project 1-1 Parser
    SimpleDBMSParser parser = new SimpleDBMSParser(System.in);
    Message.printPrompt();
    
    while (true)
    {
      try
      {
        parser.command();
      }
      catch (Exception e)
      {
        Message.printMessage(PRINT_SYNTAX_ERROR);
        SimpleDBMSParser.ReInit(System.in);
        Message.printPrompt();
      }
    }
  }
}

PARSER_END(SimpleDBMSParser)
 

SKIP : { < SPACE: " " > | <  TAB : "\t" > | < EOL : "\r\n" | "\n" | "\r" > }

/*
   According to the JavaCC's FAQ page (https://javacc.github.io/javacc/faq.html#question-3.3),
   there are three rules to identify the next token.
   1. The regular expression must describe a prefix of the remaining input stream.
   2. If more than one regular expression describes a prefix,
      then a regular expression that describes the longest prefix of the input stream is used
      (this is called the maximal munch rule).
   3. If more than one regular expression describes the longest possible prefix,
      then the regular expression that comes first in the .jj file is used.

      That is, the earlier the tokens are declared, the higher their priority is.
      Therefore, Keywords Tokens must be declared before LEGAL_IDENTIFIER in case of conflict.
*/


/*
   Keyword Tokens: The two-word tokens have been separated into the two tokens of one word.
*/
TOKEN :
{
  < EXIT : "exit" >
| < INT : "int" >
| < CHAR : "char" >
| < DATE : "date" >
| < CREATE : "create" >
| < DROP : "drop" >
| < SHOW : "show" >
| < DESC : "desc" >
| < SELECT : "select" >
| < INSERT : "insert" >
| < DELETE : "delete" >
| < TABLE : "table" >
| < TABLES : "tables" >
| < NULL : "null" >
| < PRIMARY : "primary" >
| < FOREIGN : "foreign" >
| < KEY : "key" >
| < REFERENCES : "references" >
| < AS : "as" >
| < FROM : "from" >
| < WHERE : "where" >
| < OR : "or" >
| < AND : "and" >
| < NOT : "not" >
| < IS : "is" >
| < INTO : "into" >
| < VALUES : "values" >
}   


/*
   Normal tokens
*/
TOKEN :
{
  < END : < SEMICOLON > (< SPACE > | < TAB >)* < EOL > > 
| < SEMICOLON : ";" >
| < ASTERISK : "*" >
| < LEFT_PAREN : "(">
| < RIGHT_PAREN : ")">
| < COMMA : ",">
| < DOT : ".">
| < INT_VALUE : (< SIGN >)? (< DIGIT >)+ >
| < DATE_VALUE : < NNNN > "-" < NN > "-" < NN > >
| < CHAR_STRING : < QUOTE > (< NON_QUOTE_CHARACTER >)* < QUOTE > >
| < OPERATOR : "<=" | ">=" | "!=" | "<" | ">" | "=" >
| < LEGAL_IDENTIFIER : < ALPHABET > (< ALPHABET > | < UNDERSCORE >)* >
| < #SIGN : "+" | "-" >  
| < #DIGIT : [ "0"-"9" ] >
| < #NNNN : < DIGIT > < DIGIT > < DIGIT > < DIGIT > >
| < #NN : < DIGIT > < DIGIT > >
| < #ALPHABET : [ "a"-"z", "A"-"Z" ] >
| < #UNDERSCORE : "_" >
| < #QUOTE : "'" > 
| < #NON_QUOTE_CHARACTER : < DIGIT > | < ALPHABET > | < NON_QUOTE_SPECIAL_CHARACTERS > | " " >
| < #NON_QUOTE_SPECIAL_CHARACTERS : "!" | "@" | "#" | "$" | "%" | "^" | "&" | "-" | "{" | "}"
    | "[" | "]" | "~" | "`" | "+" | "*" | "_" | "=" | "\\" | "(" | ")" | "<" | ">" | "?" | "," | "."  
    | "\n" | "\t" | "\r" | ":" | ";" | "|" | "/" >
| < UNEXPECTED: ~[] >
}


void command() :
{}
{
  queryList()
| (
    (
      < EXIT >
      (
        < SEMICOLON > | < END >
      )
    )
  | < EOF >
  )
  {
    System.exit(0);
  }
}

void queryList() :
{
  int q;
}
{
  ( 
    q = query()
    (
      (
        // When a query ends at the end.
        < END >
        {
          Message.printMessage(q);
          Message.printPrompt();
        }
      )
    | (
        // For query sequences. 
        < SEMICOLON >
        {
          Message.printMessage(q);
        }
      )
    )
  )+
}

int query() :
{
  int q;
}
{
  (
    createTableQuery()
    {
      q = Message.PRINT_CREATE_TABLE;
    }
  | dropTableQuery()
    {
      q = Message.PRINT_DROP_TABLE;
    }
  | descQuery()
    {
      q = Message.PRINT_DESC;
    }
  | selectQuery()
    {
      q = Message.PRINT_SELECT;
    }
  | insertQuery()
    {
      q = Message.PRINT_INSERT;
    }
  | deleteQuery()
    {
      q = Message.PRINT_DELETE;
    } 
  | showTablesQuery()
    {
      q = Message.PRINT_SHOW_TABLES;
    } 
  ) 
  (
    {
      return q;
    }
  )
}

/*
   CREATE TABLE START
*/
void createTableQuery() :
{}
{
  < CREATE >
  < TABLE >
  tableName()
  tableElementList()
}

void tableElementList() :
{}
{
  < LEFT_PAREN >
  tableElement()
  (
    < COMMA >
    tableElement()
  )*
  < RIGHT_PAREN >
}

void tableElement() :
{}
{
  columnDefinition()
| tableConstraintDefinition()
}

void columnDefinition() :
{}
{
  columnName()
  dataType()
  (
    < NOT >
    < NULL >
  )?
}

void tableConstraintDefinition() :
{}
{
  primaryKeyConstraint()
| referentialConstraint()
}

void primaryKeyConstraint() :
{}
{
  < PRIMARY >
  < KEY >
  columnNameList()
}

void referentialConstraint() :
{}
{
  < FOREIGN >
  < KEY >
  columnNameList()
  < REFERENCES >
  tableName()
  columnNameList()
}

void columnNameList() :
{}
{
  < LEFT_PAREN >
  columnName()
  (
    < COMMA >
    columnName()
  )*
  < RIGHT_PAREN >
}

void dataType() :
{}
{
  < INT >
| (
    < CHAR >
    < LEFT_PAREN >
    < INT_VALUE >
    < RIGHT_PAREN >
  )
| < DATE >
}

void tableName() :
{}
{
  < LEGAL_IDENTIFIER >
}

void columnName() :
{}
{
  < LEGAL_IDENTIFIER >
}
/*
   CREATE TABLE END
*/

/*
   DROP TABLE / DESC / SHOW TABLES START
*/
void dropTableQuery() :
{}
{
  < DROP >
  < TABLE >
  tableName()
}

void descQuery() :
{}
{
  < DESC >
  tableName()
}

void showTablesQuery() :
{}
{
  < SHOW >
  < TABLES >
}
/*
   DROP TABLE / DESC / SHOW TABLES END
*/

/*
   SELECT START
*/
void selectQuery() :
{}
{
  < SELECT >
  selectList()
  tableExpression()
}

void selectList() :
{}
{
  < ASTERISK >
| (
    selectedColumn()
    (
      < COMMA >
      selectedColumn()
    )*
  )
}

void selectedColumn() :
{}
{
  columnInTable()
  (
    < AS >
    columnName()
  )?
}

void period() :
{}
{
  < DOT >
}

/*
   Make a new term for duplicate expressions.
   < COLUMN IN TABLE > ::= [< TABLE NAME > < PERIOD >] < COLUMN NAME >
*/ 
void columnInTable() :
{}
{
  (
    /*
       A lookahead of 2 is used here because both < TABLE NAME > and < COLUMN NAME > are < LEGAL IDENTIFIER >.
    */
    LOOKAHEAD(2)
    tableName()
    period()
  )?
  columnName()
}

void tableExpression() :
{}
{
  fromClause()
  (
    whereClause()
  )?
}

void fromClause() :
{}
{
  < FROM >
  tableReferenceList()
}

void tableReferenceList() :
{}
{
  referedTable()
  (
    < COMMA >
    referedTable()
  )*
}

void referedTable() :
{}
{
  tableName()
  (
    < AS >
    tableName()
  )?
}

void whereClause() :
{}
{
  < WHERE >
  booleanValueExpression()
}

void booleanValueExpression() :
{}
{
  booleanTerm()
  (
    < OR >
    booleanTerm()
  )*
}

void booleanTerm() :
{}
{
  booleanFactor()
  (
    < AND >
    booleanFactor()
  )*
}

void booleanFactor() :
{}
{
  (
    < NOT >
  )?
  booleanTest()
}

void booleanTest() :
{}
{
  predicate()
| parenthesizedBooleanExpression()
}

void parenthesizedBooleanExpression() :
{}
{
  < LEFT_PAREN >
  booleanValueExpression()
  < RIGHT_PAREN >
}

void predicate() :
{}
{
  /*
     comparisonPredicate() can be 'columnInTable() compOp() ...' and
     nullPredicate() can also become 'columnInTable() nullOperation()'. 
     Since the maximum number of tokens in columnInTable() is three,
     four tokens should be lookaheaded to determine which tokens are consumed.
     But a lookahead of 4 makes the parser much slower, so I have rewritten the grammar as below.
     <PREDICATE> ::= <COLUMN IN TABLE PREDICATE> | <COMPARABLE VALUE PREDICATE>
     <COLUMN IN TABLE PREDICATE> ::= <COLUMN IN TABLE> (<COMP OP> <COMP OPERAND> | <NULL OPERATION>)
     <COMPARABLE VALUE PREDICATE> ::= <COMPARABLE VALUE> <COMP OP> <COMP OPERAND>    
  */
  columnInTablePredicate()
| comparableValuePredicate()
}

void columnInTablePredicate() :
{}
{
  columnInTable()
  (
    (
      compOp()
      compOperand()
    )
    | nullOperation() 
  )
}

void comparableValuePredicate() :
{}
{
  comparableValue()
  compOp()
  compOperand()
}

void compOperand() :
{}
{
  comparableValue()
| columnInTable()
}

void compOp() :
{}
{
  < OPERATOR >
}

void comparableValue() :
{}
{
  < INT_VALUE >
| < CHAR_STRING >
| < DATE_VALUE >
}

void nullOperation() :
{}
{
  < IS >
  (
    < NOT >
  )?
  < NULL >
}
/*
   SELECT END
*/

/*
   INSERT / DELETE START
*/
void insertQuery() :
{}
{
  < INSERT >
  < INTO >
  tableName()
  insertColumnAndSource()
}

void insertColumnAndSource() :
{}
{
  (
    columnNameList()
  )?
  valueList()
}

void valueList() :
{}
{
  < VALUES >
  < LEFT_PAREN >
  value()
  (
    < COMMA >
    value()
  )*
  < RIGHT_PAREN >
}

void value() :
{}
{
  < NULL >
| comparableValue()
}

void deleteQuery() :
{}
{
  < DELETE >
  < FROM >
  tableName()
  (
    whereClause()
  )?
}
/*
   INSERT / DELETE END
*/